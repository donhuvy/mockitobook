// Using Gradle version catalogs -- see gradle/libs.versions.toml
plugins {
    id 'java'
    id 'jacoco'
    alias(libs.plugins.versions)
    alias(libs.plugins.version.catalog.update)
}

group 'com.kousenit'
version '1.0'

// Java 11+ needed for HttpClient
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // JUnit bundle (includes vintage engine)
    testImplementation libs.bundles.junit

    // Mockito bundle (inline and JUnit Jupiter engine)
    testImplementation libs.bundles.mockito

    // AssertJ
    testImplementation libs.assertj

    // Spring test library (don't move to 3.* unless Java 17+)
    testImplementation 'org.springframework.boot:spring-boot-test:3.2.5'

    // Retrofit
    implementation libs.retrofit.core
    implementation libs.retrofit.gson

    // Make security warning go away for retrofit transitive dep
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // Jackson JSON library
    implementation libs.jackson

    // Gson
    implementation libs.gson
}

tasks.named('test', Test) {
    useJUnitPlatform()
    maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2 + 1)
}

jacocoTestReport.dependsOn(test)