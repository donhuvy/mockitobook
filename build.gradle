plugins {
    id 'java'
    id "com.github.ben-manes.versions" version "0.42.0"
    id "nl.littlerobots.version-catalog-update" version "0.5.1"
}

group 'com.kousenit'
version '1.0'

// Java 11+ needed for HttpClient
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Using Gradle version catalogs -- see gradle/libs.versions.toml

    // JUnit, including vintage engine
    testImplementation libs.bundles.junit

    // Mockito inline (includes core) and JUnit Jupiter engine
    testImplementation libs.bundles.mockito

    // AssertJ
    testImplementation libs.assertj

    // Retrofit
    implementation libs.retrofit.core
    implementation libs.retrofit.gson

    // Jackson JSON library
    implementation libs.jackson

    // Gson
    implementation libs.gson
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors() / 2 + 1
}